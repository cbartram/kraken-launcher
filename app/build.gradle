plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

def projectVersion = System.getenv("VERSION") ?: '1.0.0'
def runeLiteVersion = '1.12.6' // latest.release

version = projectVersion
project.version = projectVersion


repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
    maven {
        url = "https://repo.runelite.net"
    }
    maven {
        url 'https://jitpack.io'
    }
    mavenLocal()
}

dependencies {
    implementation("com.google.code.gson:gson:2.10.1")
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    compileOnly group: 'net.runelite', name:'client', version: runeLiteVersion
    testImplementation 'junit:junit:4.12'
    testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
    testImplementation group: 'net.runelite', name:'jshell', version: runeLiteVersion
}

testing {
    suites {
        test {
            useJUnit('4.13.2')
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

jar {
    archiveBaseName = 'kraken-launcher'
    archiveVersion = projectVersion
}

shadowJar {
    archiveBaseName = 'kraken-launcher'
    archiveVersion = projectVersion
    archiveClassifier = 'fat'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = 'com.github.cbartram'
            artifactId = 'kraken-launcher'
            version = project.version

            pom {
                name = 'Kraken Launcher'
                description = 'Kraken Launcher'
                url = 'https://github.com/cbartram/kraken-launcher'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'cbartram'
                        name = 'RuneWraith'
                        email = 'runewraith.business@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/cbartram/kraken-launcher.git'
                    developerConnection = 'scm:git:ssh://github.com/cbartram/kraken-launcher.git'
                    url = 'https://github.com/cbartram/kraken-launcher'
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cbartram/kraken-launcher")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
